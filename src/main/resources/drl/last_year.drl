package drl;

import nl.jqno.foobal.domain.Outcome;
import nl.jqno.foobal.predictoutcomes.OutcomeBuilder;

dialect "mvel"

rule "Last year's outcome"
when
	$result  : OutcomeBuilder()
	$outcome : Outcome( homeTeam == $result.homeTeam,
	                    outTeam  == $result.outTeam,
	                    $latestDate : date )
	not Outcome( homeTeam == $result.homeTeam,
	             outTeam  == $result.outTeam,
	             $latestDate < date )
then
	$result.setOutcome($outcome.homeScore, $outcome.outScore);
end

rule "There was no outcome last year"
when
	$result : OutcomeBuilder()
	not Outcome( homeTeam == $result.homeTeam,
	             outTeam  == $result.outTeam )
then
	$result.setOutcome(0, 0)
end
