package drl;

import nl.jqno.foobal.domain.*;

dialect "mvel"

rule "Last year's outcome"
when
	$result  : ScoreKeeper()
	$outcome : Outcome( homeTeam == $result.homeTeam,
	                    outTeam  == $result.outTeam,
	                    $latestDate : date )
	not Outcome( homeTeam == $result.homeTeam,
	             outTeam  == $result.outTeam,
	             $latestDate < date )
then
	$result.add("Last year", $outcome.homeScore, $outcome.outScore)
end

rule "The home team had no outcomes last year"
when
	$result : ScoreKeeper()
	not Outcome( homeTeam == $result.homeTeam )
	not Outcome( outTeam == $result.homeTeam )
then
	$result.add("No home team last year", 0, 1)
end

rule "The out team had no outcomes last year"
when
	$result : ScoreKeeper()
	not Outcome( homeTeam == $result.outTeam )
	not Outcome( outTeam == $result.outTeam )
then
	$result.add("No out team last year", 1, 0)
end

rule "There was no match last year (but both teams had matches)"
when
	$result : ScoreKeeper()
	not Outcome( homeTeam == $result.homeTeam,
	             outTeam  == $result.outTeam )
	Outcome( homeTeam == $result.homeTeam )
	Outcome( outTeam == $result.homeTeam )
	Outcome( homeTeam == $result.outTeam )
	Outcome( outTeam == $result.outTeam )
then
	$result.add("No match last year (but both teams competed)", 0, 0)
end

rule "There was no match last year, as neither team competed last year"
when
	$result : ScoreKeeper()
	not Outcome( homeTeam == $result.homeTeam,
	             outTeam  == $result.outTeam )
	not Outcome( homeTeam == $result.homeTeam )
	not Outcome( outTeam == $result.homeTeam )
	not Outcome( homeTeam == $result.outTeam )
	not Outcome( outTeam == $result.outTeam )
then
	$result.add("No match last year (because neither team competed)", 0, 0)
end